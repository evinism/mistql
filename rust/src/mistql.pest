query = _{ SOI ~ expr ~ EOI }

expr = _{ piped_expr | simple_expr }
piped_expr = { simple_expr ~ ( "|" ~ simple_expr )+ }
simple_expr = _{ function | prefixed_value | indexed_value | value }

value = _{ simple_value ~ ( infix_op ~ simple_value )* }
simple_value = _{ literal | reference | ( "(" ~ expr ~ ")" ) }
function = { fn_ident ~ value+ }
fn_ident = _{
    ident |
    ( dollar ~ "." ~ ident ) |
    ( "(" ~ simple_expr ~ ")" )
}

indexed_value = { value ~ "[" ~ (
    range | low_range | high_range | number
) ~ "]" }
range = { number ~ ":" ~ number }
low_range = { ":" ~ number }
high_range = { number ~ ":" }

prefixed_value = { prefix_op ~ expr }
prefix_op = _{ not_op }

not_op = { "!" }

infix_op = {
    "+" | "-" | "*" | "/" | "%" |
    "==" | "!=" | ">=" | ">" | "<=" | "<" |
    "&&" | "||" | "=~"
}

reference = _{ compound_reference | at | dollar | ident }
compound_reference = {
    ( at | dollar | ident | ( "(" ~ expr ~ ")" ) ) ~
    ("." ~ reference)+ }
literal = _{ object | array | string | number | bool | null }

object = {
    "{" ~ "}" |
    "{" ~ keyval ~ ("," ~ keyval)* ~ "}"
}
keyval = { key ~ ":" ~ expr }
key = _{ string | ident }

array = {
    "[" ~ "]" |
    "[" ~ expr ~ ("," ~ expr)* ~ "]"
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

bool = { "true" | "false" }
null = { "null" }

at = { "@" }
dollar = { "$" }

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }