query = _{ SOI ~ (piped_expr | simple_expr) ~ EOI }

piped_expr = { simple_expr ~ ( "|" ~ simple_expr )+ }

simple_expr = _{ function | value }

value = _{ literal | reference | ( "(" ~ piped_expr ~ ")" ) }
function = { ident ~ value+ }

reference = _{ ident | at }
literal = _{ array | string | number | bool | null }

array = {
    "[" ~ "]" |
    "[" ~ simple_expr ~ ("," ~ simple_expr)* ~ "]"
}

ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

bool = { "true" | "false" }
null = { "null" }

at = { "@" | "$.@"}

WHITESPACE = _{ " " | "\t" | "\n" | "\r" }